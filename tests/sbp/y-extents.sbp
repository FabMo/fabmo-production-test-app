&input=3
&length=24.8
&extra=1.0

' Search for the negative end of travel
PY,%(2)-&length-&extra,2,&input
PAUSE 0.1

' Back off
JY,%(2)+0.5

' Search again slowly
PY,%(2)-&extra,0.1,&input
PAUSE 0.1

' Back off again
JY,%(2)+0.5

' Record the start position
&ystart=%(2)

' Search one more time, slowly, this is a measurement
PY,&ystart-1,0.1,&input
PAUSE 0.1

' Record the distance to the switch (ycheck = 0.5)
$yn = %(2)
$yncheck=$yn-&ystart

' Jog most of the way to the other end of the table
JY,%(2)+&length-&extra
PAUSE 0.1

' Search for the positive end of travel
PY,%(2)+&extra*2,2,&input
PAUSE 0.1

' Back off
JY,%(2)-0.5

' Search again, slowwly
PY,%(2)+1.0,0.1,&input
PAUSE 0.1

' Back off
JY,%(2)-0.5

' Record the start position
&ystart=%(2)

' Search again, slowly, this is a measurement
PY,%(2)+1.0,0.1,&input
PAUSE 0.1

$yp = %(2)
$ypcheck=$yp-&ystart

JY,$yn + ($yp - $yn)/2.0

&ytravel = $yp-$yn
'PAUSE "yncheck=" + $yncheck + " ypcheck=" + $ypcheck + " yn=" + $yn + " yp=" + $yp
