' Test routine to check Z travel, proximity switch, and wiring

' Settings
&zin = 5  				' The input switch that corresponds to the Z prox switch
&zplate = 1				' The input switch that corresponds to the Z plate
&zsearch = 26			' The expected search distance

$status = 0 

' Bail in the case of being already on the prox switches
IF %(55) == 1 THEN GOTO on_prox

' Outputs (these are the persistent variables that are read out by the test app)
$zpcheck = 0			' The output of this routine, which is the fixed distance measured against the switch
$zp = 0

' Do an initial back off (in case we're already on the switch) 
' JZ,%(3)-0.5 ' Actually don't do this, because the Z is almost always going to be sitting on the table

' TODO: check to make sure the switch isn't already tripped

' Search for the switch
PZ,%(3)+&zsearch,1,&zin

' Back off
JZ,%(3)-0.5

' Approach, slowly
PZ,%(3)+1,0.1,&zin

' Back off
JZ,%(3)-0.5

' Record the initial position
&zstart = %(3)

' Final approach, slowly this time is a measurement
PZ,%(3)+1,0.1,&zin

' Record final position
$zp=%(3)
$zpcheck=$zp-&zstart

' Back off 
JZ,%(3)-1.0

' Head for the plate
PZ,%(3)-&zsearch,0.5,&zplate

' Back off
JZ,%(3)+0.5

' Approach, slowly
PZ,%(3)-1,0.1,&zplate

' Back off
JZ,%(3)+0.5

' Record the initial position
&zstart = %(3)

' Final approach, slowly this time is a measurement
PZ,%(3)-1,0.1,&zplate

' Record final position
$zn=%(3)
$zncheck=&zstart-$zn

' Jog to a sane place
JZ,%(3)+4.0
END

on_prox:
	$status = 1
	END